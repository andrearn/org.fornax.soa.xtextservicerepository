module org.xkonnex.repo.dsl.sladsl.SLADsl

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var grammarURI = "classpath:/org/xkonnex/repo/dsl/sladsl/SLADsl.xtext"
var baseName = "org.xkonnex.repo.dsl.sladsl"
var dslName = "org.xkonnex.repo.dsl.sladsl.SLADsl"
var file.extensions = "sladsl"
var projectName = "org.xkonnex.repo.dsl.sladsl"
var runtimeProject = "../${projectName}"
var rootPath = ".."

var baseMetaModel="platform:/resource/org.xkonnex.repo.dsl.basedsl/model/generated/BaseDsl.ecore"
var baseGenModel="platform:/resource/org.xkonnex.repo.dsl.basedsl/model/generated/BaseDsl.genmodel"

var semMetaModel = "platform:/resource/org.xkonnex.repo.dsl.semanticsdsl/model/generated/SemanticsDsl.ecore"
var semGenModel = "platform:/resource/org.xkonnex.repo.dsl.semanticsdsl/model/generated/SemanticsDsl.genmodel"

Workflow {
    bean = StandaloneSetup {
		platformUri = "${runtimeProject}/.."
		scanClassPath=true
//		uriMap = { to = baseMetaModel from = "http://www.xkonnex.org/repo/dsl/basedsl/BaseDsl" }
		uriMap = { to = semMetaModel from = "http://www.xkonnex.org/repo/dsl/semanticsdsl/SemanticsDsl" }
		registerEcoreFile = baseMetaModel
		registerEcoreFile = semMetaModel
		registerGenModelFile = baseGenModel
		registerGenModelFile = semGenModel
		registerGenModelFile = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
	}

	component = DirectoryCleaner {
		directory = "${runtimeProject}/src-gen"
	}
	component = DirectoryCleaner {
		directory = "${runtimeProject}/model/generated"
	}

	component = DirectoryCleaner {
		directory = "${runtimeProject}.ide/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${runtimeProject}.ui/src-gen"
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				runtimeTest = {
					enabled = false
					root = "${runtimeProject}/../../test/${projectName}.test"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				ideaPlugin = {
					enabled = false
				}
				web = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = XtextGeneratorLanguage {
			name = dslName
			fileExtensions = file.extensions
			grammarUri=grammarURI
			
			referencedResource="platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
			referencedResource=baseGenModel
			referencedResource=semGenModel
			
		    fragment = grammarAccess.GrammarAccessFragment2 {}
		    fragment = ecore.EMFGeneratorFragment2 {}
		    fragment = serializer.SerializerFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
		    }
		    fragment = resourceFactory.ResourceFactoryFragment2 {}
		    fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
		    	options = {
					classSplitting = true
				}
		    }
		    fragment = validation.ValidatorFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
				composedCheck = "org.eclipse.xtext.validation.ImportUriValidator"
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
		    }
		    fragment = scoping.ImportNamespacesScopingFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
		    }
		    fragment = exporting.QualifiedNamesFragment2 {}
//		    fragment = builder.BuilderIntegrationFragment2 {}
//		    fragment = generator.GeneratorFragment2 {}
		    fragment = formatting.Formatter2Fragment2 {}
		    fragment = ui.labeling.LabelProviderFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
		    }
		    fragment = ui.outline.QuickOutlineFragment2 {}
		    fragment = ui.outline.OutlineTreeProviderFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
		    }
		    fragment = ui.quickfix.QuickfixProviderFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
		    }
		    fragment = ui.contentAssist.ContentAssistFragment2 {
		    	generateStub = true
		    	generateXtendStub = false
		    }
//		    fragment = junit.Junit4Fragment2 {}
		    fragment = ui.refactoring.RefactorElementNameFragment2 {}
		    fragment = types.TypesGeneratorFragment2 {}
		    fragment = xbase.XtypeGeneratorFragment2 {}
//		    fragment = xbase.XbaseGeneratorFragment2 {}
		    fragment = ui.templates.CodetemplatesGeneratorFragment2 {}
		    fragment = ui.compare.CompareFragment2 {}
		    fragment = idea.parser.antlr.XtextAntlrIDEAGeneratorFragment {
		    	options = {
					classSplitting = true
				}
		    }
//		    fragment = idea.IdeaPluginGenerator {}
		    fragment = web.WebIntegrationFragment {
		        framework = "Ace"
		    }			
		}
	}

}
