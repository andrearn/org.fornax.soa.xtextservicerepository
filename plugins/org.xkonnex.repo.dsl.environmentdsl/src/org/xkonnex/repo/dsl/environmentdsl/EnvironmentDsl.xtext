/*
Copyright (c) 2010-2018 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
*/
grammar org.xkonnex.repo.dsl.environmentdsl.EnvironmentDsl with org.xkonnex.repo.dsl.basedsl.BaseDsl
generate environmentDsl "http://www.xkonnex.org/repo/dsl/environmentdsl/EnvironmentDsl"

import "http://www.xkonnex.org/repo/dsl/basedsl/BaseDsl" as  baseDsl
import "http://www.xkonnex.org/repo/dsl/semanticsdsl/SemanticsDsl" as  semanticsDsl
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

EnvModel:
	(imports+=EnvImport)*
	(environments+=Environment)*;
	
EnvImport :
	'import' importedNamespace=QualifiedNameWithWildCard ; 
	
EnvFileImport:
	'import' importURI=STRING ;
	
Environment:
	(type=EnvironmentType) name=ID '{'
		(hosts+=Host)*
		(executables+=AbstractExecutable)*
		
		/* for centralized architectures, should be avoided */
		('default' 'ESB' defaultESB=[ESB|QualifiedName])? 
		('default' 'Registry' defaultRegistry=[Registry|QualifiedName])? 
		('default' 'AppServer' defaultAppServer=[AppServer|QualifiedName])? 
		('default' 'Broker' defaultBroker=[Broker|QualifiedName])? 
		('default' 'InfrastructureManager' defaultManager=[InfrastructureManager|QualifiedName])? 
	'}';
	
enum EnvironmentType: Prod = "Production-Environment" | Staging="Staging-Environment" | Integration="Integration-Environment" | Test="Test-Environment" | Dev="Development-Environment" | Local="Local-Environment" | InfrastructureLibrary="Infrastructure-Library" | Other="Environment";

EnvironmentComponent returns baseDsl::Component: Connector|AbstractExecutable|SecurityProtocol|Host|ConfigurationScript|Database;

Host: BareHost|VM|Container;
AbstractExecutable: Server | Cluster | Executable | Gateway | IdentityAccessManager;

	

Server: ESB | Registry | ProcessServer | AppServer | Broker | WebServer | Database | SAP | ExtensibleServer | InfrastructureManager;

ExtensibleServer:
	(isDefault?='default')? 'server' name=ID 'type' type=[jvmTypes::JvmType|QualifiedName] '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}'
;/* An Enterprise Service Bus */
ESB: 
	'esb' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('deployDir' deployDir=STRING )?
		('adminUrl' adminUrl=STRING )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';

Gateway: 
	(isDefault?='default')? 'gateway' name=ID 'type' type=[jvmTypes::JvmType|QualifiedName] '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('deployDir' deployDir=STRING )?
		('adminUrl' adminUrl=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';

/* A service registry */
Registry:
	'registry' name=ID  ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('deployDir' deployDir=STRING )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		//deprecated
		('kind' kind=RegistryKind )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';
	
//Configuration servers like Zookeeper, Deployment automation server, monitoring system
InfrastructureManager:
	'infrastructure-manager' name=ID 'type' type=[jvmTypes::JvmType|QualifiedName] '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}'
;
enum RegistryKind : UDDI | HTTP | File | Native;

RegistryService: JNDIRegistry | COSNamingRegistry | RMIRegistry | IOR | UDDIRegistry;

/* A Java JNDI registry */
JNDIRegistry: 
	'jndi' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
		'jndi-url' url=STRING 
		('InitialContextFactory' initialContextFactory = STRING)?
		(clustered?='clustered')?
	'}'
;

/* A CORBA COS naming based registry */
COSNamingRegistry:
	'COS-Naming' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
		(clustered?='clustered')?
	'}'
;

/* A plain Java RMI registry, where RMI capable objects are registered */
RMIRegistry:
	'RMI-Registry' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
	'}'
;

/* A interface object repository, where CORBA objects are registered */
IOR:
	'Corba-IOR' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
	'}'
;

/* A UDDI registry used to retrieve WDSLs of webservices */
UDDIRegistry:
	'UDDI' '{'
		'url' url = STRING
		('secure-url' secureUrl = STRING)?
	'}'
;

/* An application server */	
AppServer: 
	'appserver' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=EmailOrUsername)+ )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('deployDir' deployDir=STRING  )?
		('adminUrl' adminUrl=STRING  )?
		(dataSources+=DataSource)*
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';

/* A HTTP webserver, e.g. Apache */
WebServer: 
	'webserver' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=EmailOrUsername)+ )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('deployDir' deployDir=STRING  )?
		('baseUrl' baseUrl=STRING  )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';
	
/* A business process server */
ProcessServer:
	'processserver'  name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=EmailOrUsername)+ )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('deployDir' deployDir=STRING  )?
		('deployUser' deployUser=EmailOrUsername )?
		('adminUrl' adminUrl=STRING  )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';

/* A message broker */
Broker: 
	'broker' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )? 
		('admins' (admins+=EmailOrUsername)+ )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING)? 
		('deployDir' deployDir=STRING )?
		('adminUrl' adminUrl=STRING )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}'; 

Database:  
	'database' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=EmailOrUsername)+ )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )? 
		('dataDir' dataDir=STRING )? 
		('adminUrl' adminUrl=STRING  )?
		('jdbcUrl' jdbcUrl=STRING )?
		('database-type' serverType=STRING )?
		('database-version' serverVersion=STRING )?
		('{' (schema+=DatabaseSchema)+ '}')? 
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';

/* A SAP system */
SAP:  
	'sap' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=EmailOrUsername)+ )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		'system-id' systemId = INT 
		('dir' home=STRING )?
		('dataDir' dataDir=STRING )?
		('adminUrl' adminUrl=STRING )? 
		('{' (client+=SapClient)+ '}')? 
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';
	
SapClient: 
	'client' name=ID '{'
		'client-ID' client=INT 
		'owner' owner=STRING 
		('accountingArea' accountingArea=STRING)?
	'}';
	
DataSource:
	'data-source' name=ID '{'
		'jndi-name' jndiName=STRING 
		'schema' schema=[DatabaseSchema|QualifiedName] 
	'}';
	
Executable:
	'executable' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		(version=Version)?
		('identifier' dslCode=STRING )?
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('dir' home=STRING )?
		('baseUrl' baseUrl=STRING  )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}';
	
/** @deprecated */	
enum DatabaseKind : Other | Oracle | DB2 | MSSQL | MySQL | Postgres | Sybase | HyperSonic | Ingres | Adabas | Derby | MongoDB | Hadoop | CouchDB | Neo4J;

DatabaseSchema: 
	'schema' name=ID 'owner' owner=STRING;

/*
 * A protocol, connector available on the server that support a specific protocol. E.g. used to expose service endpoints
 */
Connector:
	SOAPHTTP | SOAPJMS | REST | EJB | JMS | AMQP | IIOP | RMI | Tuxedo | RFC | FTP | IMAP | POP3 | SMTP | HTTP | TCP | ExtensibleConnector
;

ExtensibleConnector:
	(isDefault?='default')? 'connector' name=ID 'type' type=[jvmTypes::JvmType|QualifiedName] '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('protocol-versions' (version+=STRING)+)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
	'}'
;

enum EndpointVisibility: private | public | external;

/* Connector for SOAP over HTTP */
SOAPHTTP:
	{SOAPHTTP}
	(isDefault?='default')? 'SOAP/HTTP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
		('context-root' contextRoot=STRING)?
		('implementation' implementation=STRING)?
	'}';

/* Connector for SOAP over JMS */
SOAPJMS:
	(isDefault?='default')? 'SOAP/JMS' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		'port' port=INT
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
		connectionSpec=JCAConnectionSpec
		('implementation' implementation=STRING)?
	'}';

/* A HTTP REST connector */
REST:
	{REST}
	(isDefault?='default')? 'REST' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
		('context-root' contextRoot=STRING)?
	'}';

/* An EJB connector. Defines the RMI or IIOP ports */
EJB:
	{EJB}
	(isDefault?='default')? 'EJB' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
	'}'
;

/* A (CORBA) IIOP connector */
IIOP:
	{IIOP}
	(isDefault?='default')? 'IIOP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
		(connectionSpec=JCAConnectionSpec)?
	'}'
;

/* A Java RMI connector */
RMI:
	{RMI}
	(isDefault?='default')? 'RMI' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(connectionSpec=JCAConnectionSpec)?
	'}'
;

/* A Tuxedo RPC connector */
Tuxedo:
	{Tuxedo}
	(isDefault?='default')? 'Tuxedo' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
	'}'
;

/* A JMS connector */
JMS:
	(isDefault?='default')? 'JMS' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
		connectionSpec=JCAConnectionSpec
		('implementation' implementation=STRING)?
	'}';

/* An AMQP based messaging connector */
AMQP:
	{AMQP}
	(isDefault?='default')? 'AMQP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
		(connectionSpec=JCAConnectionSpec)?
		('implementation' implementation=STRING)?
	'}'
;

/* A SAP RFC connector */
RFC: 
	{RFC}
	(isDefault?='default')? 'RFC' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(connectionSpec=JCAConnectionSpec)?
	'}';

/* A FTP file transfer connector */
FTP:
	{FTP}
	(isDefault?='default')? 'FTP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
	'}';

/* An IMAP mail connector */
IMAP:
	{IMAP}
	(isDefault?='default')? 'IMAP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
	'}';

/*
 * A POP3 mail connector
 */
POP3:
	{POP3}
	(isDefault?='default')? 'POP3' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
	'}';

JCAConnectionSpec:
	(isDefault?='default')? 'JCA-connection-spec' name=ID '{'
		'connection-factory-JNDI-name' connectionFactoryJNDI = STRING
		('connection-factory-class' connectionFactoryClass = STRING)?
	'}';

/*
 * An SMTP mail sender connector
 */
SMTP:
	{SMTP}
	(isDefault?='default')? 'SMTP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort = INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(requiresAuth ?= 'requires-auth' )?
	'}';

/*
 * A plain HTTP connector
 */
HTTP:
	{HTTP}
	(isDefault?='default')? 'HTTP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		('port' port=INT)?
		('secured-port' securedPort = INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		('context-root' contextRoot = STRING)?
		(requiresAuth ?= 'requires-auth' )?
	'}';
	
/*
 * A plain TCP connector
 */
TCP:
	(isDefault?='default')? 'TCP' name=ID '{'
		('endpoint-visibility' endpointVisibility=EndpointVisibility)?
		'port' port = INT
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}';

	
/* A host, i.e. hardware */
BareHost:
	'host' name=ID '{'
		'full-qualified-name' fqn=STRING 
		('ip-address' ip=STRING)? 
		('identifier' dhsCode=STRING )?
		('admins' admins+=EmailOrUsername)?
		('virtual-machines' '{'
			vm+=[VM|QualifiedName]+
		'}')?
		('containers' '{'
			container+=[Container|QualifiedName]+
		'}')?
	'}';

Cluster:
	(isDefault?='default')? 'cluster' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		('server-type' serverType=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		(
			'management-servers' '{'
				(managementServers+=[InfrastructureManager|QualifiedName])+
			'}'
		)?
		(iam=IAMReference)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
	'}';

VM:
	'virtual-machine' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])?'{'
		(image=Image)?
		'full-qualified-name' fqn=STRING 
		('ip-address' ip=STRING)? 
		('identifier' dhsCode=STRING )?
		('admins' admins+=EmailOrUsername)?
		('containers' '{'
			container+=[Container|QualifiedName]+
		'}')?
		portMappings+=PortMapping*
	'}';
	
Container:
	'container' name=ID ('type' type=[jvmTypes::JvmType|QualifiedName])? '{'
		(image=Image)?
		'full-qualified-name' fqn=STRING 
		('ip-address' ip=STRING)? 
		('identifier' dhsCode=STRING )?
		portMappings+=PortMapping*
	'}';
	
PortMapping: 'map-port' sourcePort=INT 'to' targetPort=INT;

Image:
	'image' name=ID '{'
		version=Version
		("URL" url = STRING)?
		(script = ConfigurationScript)?
	'}'
;

ConfigurationScript:
	'config-script' name=STRING 'type' type=[jvmTypes::JvmType|QualifiedName]
;

IdentityAccessManager:
	'identity-access-manager' name=ID 'type' type=[jvmTypes::JvmType|QualifiedName] '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=EmailOrUsername )? 
		'host' (host=[Host|QualifiedName] | hostAddress=STRING)
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		('configuration' '{'
			(assignment+=Assignment)+ 
		'}')?
		('tags' (tags+=[semanticsDsl::Tag|TagName])+ )*
	'}'
;

IAMReference: 'use-IAM' identityAccessManager=[IdentityAccessManager|QualifiedName] 
;
	
SecurityProtocol: type = [jvmTypes::JvmType|QualifiedName];

