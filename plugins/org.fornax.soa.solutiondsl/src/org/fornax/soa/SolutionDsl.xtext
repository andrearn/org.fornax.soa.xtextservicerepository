/*
Copyright (c) 2010-2011 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
    Christian Schneider	- reviews
*/
grammar org.fornax.soa.SolutionDsl with org.fornax.soa.basedsl.SOABaseDsl

generate solutionDsl "http://www.fornax.org/soa/SolutionDsl"

import "http://www.fornax.org/soa/basedsl/SOABaseDsl" as basedsl
import "http://www.fornax.org/soa/SemanticsDsl" as semanticsDsl
import "http://www.fornax.org/soa/ServiceDsl" as serviceDsl
import "http://www.fornax.org/soa/BusinessDsl" as businessDsl
import "http://www.fornax.org/soa/profiledsl/SOAProfileDsl" as profileDsl

Model:
	(imports+=Import)*
	(solution+=Solution)*;
	
	
Solution:
	'solution' name=QualifiedName '{'
		('domain' domain = [businessDsl::Domain|QualifiedName] )?
		version=Version
		'lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName]
		(productVersion=ProductVersion)?
		(additionalInfo=AdditionalInformation)?
		('requires' requires+=ServiceRef )*
		(features+=Feature)*
		(modules+=Module)*
		('contacts' '{'
			('owner' owner=[basedsl::User|QualifiedName])?
			('responsible' responsible += [basedsl::User|QualifiedName])*
			('budgeting' budgeting = [basedsl::User|QualifiedName])?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )?
	'}';

Module:
	'module' name=ID '{'
		'kind' kind=ModuleKind 
		(version=Version)?
		(additionalInfo=AdditionalInformation)?
		('requires' requires+=ServiceRef )*
		(features+=Feature)*
		(modules+=Module)*
		('contacts' '{'
			('owner' owner=[basedsl::User|QualifiedName])?
			('responsible' responsible += [basedsl::User|QualifiedName])*
			('budgeting' budgeting = [basedsl::User|QualifiedName])?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )?
	'}';
	
enum ModuleKind : UI | BusinessLogic | Persistence | Security | Batch | OTHER;

Feature:
	(nonFunctional?='non-functional')? 'feature' name=ID '{'
		('use-cases' (useCase+=STRING)+ )?
		('description' description=STRING )?
		(additionalInfo=AdditionalInformation)?
		('uses-capabilities' '{'
			(usesCapabilities+=CapabilityRef)+ 
		'}')?
		('requires' requires+=ServiceRef )*
		('subscribes-to-event' (event+=EventRef)(',' event+=EventRef)* )?
		('contacts' '{'
			('owner' owner=[basedsl::User|QualifiedName])?
			('responsible' responsible += [basedsl::User|QualifiedName])*
			('budgeting' budgeting = [basedsl::User|QualifiedName])?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )?
		(features+=Feature)*
	'}';
	
ServiceRef:
	service=[serviceDsl::Service|QualifiedName] versionRef=VersionRef (isResponseHandler?='handling responses')? ('calling' (calledOperations+=SimpleOperationRef) (',' calledOperations+=SimpleOperationRef)*)?;


SimpleOperationRef: 
	operation=[serviceDsl::Operation|QualifiedName];

	
CapabilityRef:
	capability=[businessDsl::Capability|QualifiedName] versionRef=VersionRef;

	
Version:
	'version' (version=VersionId)  ;
	
ProductVersion returns Version:
	'product-version' (version=VersionId)  ;
	
EventRef:
	event=[serviceDsl::Event|QualifiedName] versionRef=VersionRef;
	
	