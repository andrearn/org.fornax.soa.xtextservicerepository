/*
Copyright (c) 2010-2018 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
    Christian Schneider	- reviews
*/
grammar org.xkonnex.repo.dsl.solutiondsl.SolutionDsl with org.xkonnex.repo.dsl.basedsl.BaseDsl

generate solutionDsl "http://www.xkonnex.org/repo/dsl/solutiondsl/SolutionDsl"

import "http://www.xkonnex.org/repo/dsl/basedsl/BaseDsl" as basedsl
import "http://www.xkonnex.org/repo/dsl/semanticsdsl/SemanticsDsl" as semanticsDsl
import "http://www.xkonnex.org/repo/dsl/servicedsl/ServiceDsl" as serviceDsl
import "http://www.xkonnex.org/repo/dsl/businessdsl/BusinessDsl" as businessDsl
import "http://www.xkonnex.org/repo/dsl/profiledsl/ProfileDsl" as profileDsl
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

Model:
	imports+=Import*
	solution+=Solution*;

SolutionComponent returns basedsl::Component: ModuleKind;
	
	
Solution:
	'solution' name=QualifiedName '{'
		('domain' domain = [businessDsl::Domain|QualifiedName] )?
		version=Version
		'lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName]
		(productVersion=ProductVersion)?
		(additionalInfo=AdditionalInformation)?
		('requires' requires+=InterfaceRef )*
		('requires-channel' requiresChannel+=[serviceDsl::Channel|QualifiedName] )*
		(features+=Feature)*
		(modules+=Module)*
		contactInfo=ContactInfo?
		('tags' (tags+=[semanticsDsl::Tag|TagName])*)?
		('subscriptions' '{'
			subscriptions += EmailOrUsername*
		'}')?
	'}';

Module:
	kind=ModuleKind? 'module' name=ID '{'
		(version=Version)?
		(license=License)?
		(additionalInfo=AdditionalInformation)?
		('requires' requires+=InterfaceRef )*
		('requires-channel' requiresChannel+=[serviceDsl::Channel|QualifiedName] )*
		(features+=Feature)*
		(modules+=Module)*
		
		contactInfo=ContactInfo?
		('tags' (tags+=[semanticsDsl::Tag|TagName])*)?
		('subscriptions' '{'
			subscriptions += EmailOrUsername*
		'}')?
	'}';
	
ModuleKind : type=[jvmTypes::JvmType|QualifiedName];

Feature:
	(nonFunctional?='non-functional')? 'feature' name=ID '{'
		('use-cases' (useCase+=STRING)+ )?
		('description' description=STRING )?
		(additionalInfo=AdditionalInformation)?
		('uses-capability' '{'
			(usedCapabilities+=CapabilityRef)+ 
		'}')?
		('realizes-capability' '{'
			(supportedCapabilities+=CapabilityRef)+ 
		'}')?
		('supports-capability' '{'
			(realizedCapabilities+=CapabilityRef)+ 
		'}')?
		('requires' requires+=InterfaceRef )*
		('requires-channel' requiresChannel+=[serviceDsl::Channel|QualifiedName] )*
		(features+=Feature)*
		
		contactInfo=ContactInfo?
		('tags' (tags+=[semanticsDsl::Tag|TagName])*)?
		('subscriptions' '{'
			subscriptions += EmailOrUsername*
		'}')?
	'}';
	
InterfaceRef:
	interface=[serviceDsl::Interface|QualifiedName] versionRef=VersionRef (ServiceRefCallFragment)?;

fragment ServiceRefCallFragment: '{'
		'calling' '{'
			(calledOperations+=CallOperationRef) (',' calledOperations+=CallOperationRef)* 
		'}'
	'}'
;

CallOperationRef: operation=[serviceDsl::AbstractOperation|QualifiedName] ('handle-response-with' responseHandler=OperationRef)?;


/*
 * A reference to a service operation with a version constraint
 */	
OperationRef:
	operation=[serviceDsl::Operation|QualifiedName] versionRef=VersionRef;

//SimpleOperationRef: 
//	operation=[serviceDsl::Operation|QualifiedName];

	
CapabilityRef:
	capability=[businessDsl::Capability|QualifiedName] versionRef=VersionRef;

ProductVersion returns Version:
	'product-version' (version=VersionId)  ;
	
EventRef:
	event=[serviceDsl::Event|QualifiedName] versionRef=VersionRef;
	
	