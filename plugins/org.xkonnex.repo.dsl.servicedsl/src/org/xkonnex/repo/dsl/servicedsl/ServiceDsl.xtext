/*
Copyright (c) 2010-2017 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
    Christian Schneider	- reviews
    Krunoslav Markotic	- reviews
	Dennis Hauser		- reviews
    Vitaly Markin
    Liane Lenke
*/
grammar org.xkonnex.repo.dsl.servicedsl.ServiceDsl with org.xkonnex.repo.dsl.basedsl.BaseDsl

generate serviceDsl "http://www.xkonnex.org/repo/dsl/servicedsl/ServiceDsl"

import "http://www.xkonnex.org/repo/dsl/basedsl/BaseDsl" as basedsl
import "http://www.xkonnex.org/repo/dsl/profiledsl/ProfileDsl" as profileDsl
import "http://www.xkonnex.org/repo/dsl/semanticsdsl/SemanticsDsl" as semanticsDsl
import "http://www.xkonnex.org/repo/dsl/businessdsl/BusinessDsl" as businessDsl
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ServiceModel:
	(imports+=Import)*
	(namespaces+=ServiceNamespace)*
	(types+=Type)*;

ServiceNamespace returns basedsl::Namespace: OrganizationNamespace | SubNamespace;	
/*
 * The toplevel namespace representing an organization
 */
OrganizationNamespace :
	'org-namespace' name= QualifiedName ('shortname' prefix=STRING)? '{'
		('URI' uri=STRING)?
		('metadata' '{')?
			('architecture-profile' profile = [profileDsl::Profile|QualifiedName])?
		('}')?
		(subNamespaces+=SubNamespace)*
	'}' ;

SubNamespace : DomainNamespace | InternalNamespace;
	
/* Namespace of a canonical domain subject to SOA governance rules*/
DomainNamespace:
	'domain-namespace' name=QualifiedName ('shortname' prefix=STRING)? '{'
		('metadata' '{'
			('domain' domain=[businessDsl::Domain|QualifiedName])?
			(version=Version  )?
			('URI' uri=STRING)?
			('architecture-profile' profile = [profileDsl::Profile|QualifiedName])?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}')?
		
		objects += ServiceObject*

//		(types+=Type)*
//		(resources += Resource)*
//		(services+=Service)*
//		(exceptions+=Exception)*
//		(commands+=Command)*
//		(events+=Event)*
//		(channels+=Channel)*
		
		(interalNamespaces += InternalNamespace)*
	'}';

/* Namespace for internal use with no restrictions */
InternalNamespace:	
	'internal-namespace' name=QualifiedName ('shortname' prefix=STRING)? '{'
		('metadata' '{'
			(version=Version)?
			('URI' uri=STRING)?
			('architecture-profile' profile = [profileDsl::Profile|QualifiedName])?
//			('namespace-url' namespaceUrl = STRING)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}')?
		
		objects += ServiceObject*

//		(types+=Type)*
//		(resources += Resource)*
//		(services+=Service)*
//		(exceptions+=Exception)*
//		(commands+=Command)*
//		(events+=Event)*
//		(channels+=Channel)*
		(interalNamespaces+=InternalNamespace)*
		
	'}';
		
Type:
	VersionedType;

VersionedType:
	Enumeration | DataObject ;		

DataObject: AbstractBusinessObject | QueryObject;

AbstractBusinessObject: BusinessObject | Aggregate | Entity | ValueObject;

ServiceObject: Type | Resource | Service | Exception | Command | Event | Channel;

/*
 * A complex datatype representing a business entity of the domain model. 
 * BusinessObjects are versioned.
 */
BusinessObject:
	(deprecated?='@deprecated')? 
	(abstract?='abstract')? 'businessObject'  name=ID ('extends' superObject=AbstractBusinessObjectRef)? '{' 
		'metadata' '{'
			version=Version 
			('replaces' replaces=AbstractBusinessObjectRef)?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(
				(lifecycleTrigger=LifecycleTrigger)?
			&	('provided-contract' providedContractUrl=STRING)?
			)
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(properties+=Property)*
	'}';
	
/*
 * A complex datatype representing a business entity of the domain model. 
 * BusinessObjects are versioned.
 */
ValueObject:
	(deprecated?='@deprecated')? 
	(abstract?='abstract')? 'valueObject'  name=ID ('extends' superObject=AbstractBusinessObjectRef)? '{' 
		'metadata' '{'
			version=Version 
			('replaces' replaces=AbstractBusinessObjectRef)?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(
				(lifecycleTrigger=LifecycleTrigger)?
			&	('provided-contract' providedContractUrl=STRING)?
			)
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(properties+=Property)*
	'}';

/*
 * A complex datatype representing a aggregate root entity of the domain model. 
 * Aggregates are versioned.
 */
Aggregate:
	(deprecated?='@deprecated')? 
	(abstract?='abstract')? 'aggregate'  name=ID ('extends' superObject=AbstractBusinessObjectRef)? '{' 
		'metadata' '{'
			version=Version 
			('replaces' replaces=AbstractBusinessObjectRef)?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(
				(lifecycleTrigger=LifecycleTrigger)?
			&	('provided-contract' providedContractUrl=STRING)?
			)
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(properties+=Property)*
		(operations+=Operation)*
	'}';
	
/*
 * A complex datatype representing a aggregate root entity of the domain model. 
 * Aggregates are versioned.
 */
Entity:
	(deprecated?='@deprecated')? 
	(abstract?='abstract')? 'entity'  name=ID ('extends' superObject=AbstractBusinessObjectRef)? '{' 
		'metadata' '{'
			version=Version 
			('replaces' replaces=AbstractBusinessObjectRef)?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(
				(lifecycleTrigger=LifecycleTrigger)?
			&	('provided-contract' providedContractUrl=STRING)?
			)
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(properties+=Property)*
	'}';

/* A QueryObject defined an object that is used as query template for looking up 
 * BusinessObject instances. All properties are considered optional.
 */	
QueryObject:
	(deprecated?='@deprecated')? 
	'queryObject'  name=ID ('extends' superObject=QueryObjectRef)? '{' 
		'metadata' '{'
			version=Version 
			('replaces' replaces=QueryObjectRef)?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(
				(lifecycleTrigger=LifecycleTrigger)?
			&	('provided-contract' providedContractUrl=STRING)?
			)
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(properties+=Property)*
	'}';

/*
 * A resource supporting operations named GET, .
 */
Resource:
	(deprecated?='@deprecated')? 
	visibility=Visibility 'resource' name=ID '{' 
		'metadata' '{'
			version=Version
			('replaces' replaces=ResourceRef)?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			('short-description' shortDescription=STRING)? 
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			('service-category' category=[profileDsl::ServiceCategory | QualifiedName])?
			/* To what extend is a service stateful or stateless */ 
			('statefulness' statefulness=Statefulness)?
			/* are data persisted by the service ? */ 
			('persistence' persistence=Persistence )? 
			(additionalInfo=AdditionalInformation)?
			(
				/* The business capabilities the service fully implements */
				('realizes-capability' '{'(realizedCapabilities+=CapabilityRef)+ '}')?
				/* The business capabilities that are supported but not fully implemented by this service alone */	
			&	('supports-capability' '{'(supportedCapabilities+=CapabilityRef)+ '}')?
				/* 
				 * A technical service contract to be used instead of the one generation from this service definition.
				 * This should only be used with private services, as the technical contract might not meet the architectural
				 * rules that apply to public and domain scoped services
				 */	
			&	('provided-contract' providedContractUrl=STRING )? 
			&	(lifecycleTrigger=LifecycleTrigger)?
				/*
				 * The message header definition being used. Overrides the default from the architecture profile
				 */
			&	('message-headers' '{'
					('request-message-header' requestMessageHeader = MessageHeaderRef )?
					('response-message-header' responseMessageHeader = MessageHeaderRef )?
					('publish-message-header' publishMessageHeader = MessageHeaderRef )?
				'}')?

			)
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		('base-URI' uri=STRING)?
		(operations+=ResourceOperation)+
		(resources+=Resource)*
	'}';

	
ResourceRef:
	resource=[Resource|QualifiedName] versionRef=VersionRef;
	

/* A reference to a type */
TypeRef : DataTypeRef | AbstractVersionedTypeRef;

/* A reference to a simple datatype */
DataTypeRef: 
	type=[profileDsl::DataType|QualifiedName] (many?='[]' ('as' set?='set')? )? ('content-type' contentType = STRING)?;

AbstractVersionedTypeRef:
	VersionedTypeRef | EnumTypeRef | DataObjectRef
;

/* A version constrained reference to any kind of VersionedType */
VersionedTypeRef:
	type=[VersionedType|QualifiedName] (many?='[]' ('as' set?='set')? )? versionRef=VersionRef;

/* A version constrained reference to an Enumeration */	 
EnumTypeRef:
	type=[Enumeration|QualifiedName] versionRef=VersionRef;

DataObjectRef:
	AbstractBusinessObjectRef | QueryObjectRef;

/* A version constrained reference to a BO */
AbstractBusinessObjectRef returns DataObjectRef:
	type=[AbstractBusinessObject|QualifiedName] versionRef=VersionRef;

/* A version constrained reference to a QueryObject */
QueryObjectRef returns DataObjectRef:
	type=[QueryObject|QualifiedName] versionRef=VersionRef;

fragment PatternRestriction: 'pattern' pattern=STRING;

/* A property field of an BusinessObject or QueryObject */	
Property:
	(deprecated?='@deprecated')? 
	(
		(((optional?='optional')? (weak?='weak')?) | (isBusinessKey?='business-key')? | (isProvidedKey?='provided-key')?) 
		(readonly?='read-only')?
	)
	((historizingKey?='historizing-key') | (historized?='historized') | (versionIndependantKey?='version-independant-key'))? 
	name=ID ':' type=TypeRef PatternRestriction? DefaultValue?;

	
	
SimpleAttribute:
	(deprecated?='@deprecated')? (optional?='optional')? name=ID ':' type=TypeRef;
	
Enumeration:
	(deprecated?='@deprecated')? 
	'enum' name=ID '{'
		'metadata' '{'
			version=Version  
			('replaces' replaces=EnumTypeRef )?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(
				(lifecycleTrigger=LifecycleTrigger)?
			&	('provided-contract' providedContractUrl=STRING)?
			)
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+ )?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(literals+=EnumLiteral )*
	'}';
	
EnumLiteral: 
	(deprecated?='@deprecated')? name=ID;
	
// -- Technical eternal entities


Interface: Aggregate|Service|Resource;
Service:
	(deprecated?='@deprecated')? 
	visibility=Visibility 'service' name=ID '{'
		'metadata' '{'
			version=Version  
			/*
			 * Used in conjunction with renaming the service. Points to the previous service version
			 */
			('replaces' replaces=ServiceRef )?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName])?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			('service-category' category=[profileDsl::ServiceCategory | QualifiedName])?
			/* To what extend is a service stateful or stateless */ 
			('statefulness' statefulness=Statefulness)?
			/* are data persisted by the service ? */ 
			('persistence' persistence=Persistence )? 
			(additionalInfo=AdditionalInformation)?
			(
				/* The business capabilities the service fully implements */
				('realizes-capability' '{'(realizedCapabilities+=CapabilityRef)+ '}')?
				/* The business capabilities that are supported but not fully implemented by this service alone */	
			&	('supports-capability' '{'(supportedCapabilities+=CapabilityRef)+ '}')?
				/* 
				 * A technical service contract to be used instead of the one generation from this service definition.
				 * This should only be used with private services, as the technical contract might not meet the architectural
				 * rules that apply to public and domain scoped services
				 */	
			&	('provided-contract' providedContractUrl=STRING )? 
			&	(lifecycleTrigger=LifecycleTrigger)?
				/*
				 * The message header definition being used. Overrides the default from the architecture profile
				 */
			&	('message-headers' '{'
					('request-message-header' requestMessageHeader = MessageHeaderRef )?
					('response-message-header' responseMessageHeader = MessageHeaderRef )?
					('publish-message-header' publishMessageHeader = MessageHeaderRef )?
				'}')?

			)
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			/*
			 * Semantic tagging
			 */
			('tags' (tags+=[semanticsDsl::Tag])+)?
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		/*
		 * The service operations
		 */
		(operations+=Operation)*
	'}';

AbstractOperation: Operation | ResourceOperation;
/*
 * A service operation
 */
Operation:
	(deprecated?='@deprecated')? verb=Verb? (mep=MEP)? (transactional?='transactional')? name=ID '(' (parameters+=Parameter)?(',' parameters+=Parameter)* ')' 
	('returns' (return+=Parameter) (',' return+=Parameter)*|('returns' return+=SingleReturnParameter))? 
	('throws' (throws+=ExceptionRef)?(',' throws+=ExceptionRef)*)? '{'
		('accepts-content-type' (contentType += STRING)(',' contentType += STRING)*)?
		(considerationSpec=ConsiderationSpec)?
		('statefulness' statefulness=Statefulness (updating?='updating')? )?
		('transaction-spec' transactionSpec=TransactionSpec)?
		/*
		 * overrides the default from the service
		 */ 
		('persistence' persistence=Persistence )? 
		/*
		 * Overrides the message header declaration from the architecture profile or service for this operation
		 */
		('message-headers' '{'
			('request-message-header' requestMessageHeader = MessageHeaderRef )?
			('response-message-header' responseMessageHeader = MessageHeaderRef )?
			('publish-message-header' publishMessageHeader = MessageHeaderRef )?
		'}')?
		/*
		 * Services used by this service
		 */
		('requires' requires+=RequiredInterfaceRef )*
		('requires-channel' requiresChannel+=[Channel|QualifiedName] )*
		('links' '{'
			links+=Link+
		'}')?
		/*
		 * The operation that is capable to revert/compensate the effect of this operation
		 */
		('compensated-by' compensatedBy+=OperationRef )?
		('realizes-capability' '{'(realizedCapabilities+=CapabilityRef)+ '}')?	
		('supports-capability' '{'(supportedCapabilities+=CapabilityRef)+ '}')?	
		('tags' (tags+=[semanticsDsl::Tag])+)*
	'}';
	/*Callback is used optionally. In case of callback patterns, either the explicitly defined callback is used or the callback operation derived by convention. The convention is based on the name of the operation + "Callback" */

ResourceOperation:
	(deprecated?='@deprecated')? verb=Verb uri=STRING (mep=MEP)? (transactional?='transactional')? name=ID '(' (parameters+=Parameter)?(',' parameters+=Parameter)* ')' 
	('returns' '{'
		(response+=Response) (response+=Response)*
	'}'
	)?
	('throws' '{'
		(throws+=ErrorResponse)? (throws+=ErrorResponse)*
	'}'
	)? '{'
		('accepts-content-type' (contentType += STRING)(',' contentType += STRING)*)?
		(considerationSpec=ConsiderationSpec)?
		('statefulness' statefulness=Statefulness (updating?='updating')? )?
		('transaction-spec' transactionSpec=TransactionSpec)?
		/*
		 * overrides the default from the service
		 */ 
		('persistence' persistence=Persistence )? 
		/*
		 * Overrides the message header declaration from the architecture profile or service for this operation
		 */
		('message-headers' '{'
			('request-message-header' requestMessageHeader = MessageHeaderRef )?
			('response-message-header' responseMessageHeader = MessageHeaderRef )?
			('publish-message-header' publishMessageHeader = MessageHeaderRef )?
		'}')?
		/*
		 * Services used by this service
		 */
		('requires' requires+=RequiredInterfaceRef )*
		('requires-channel' requiresChannel+=[Channel|QualifiedName] )*
		('links' '{'
			links+=Link+
		'}')?
		/*
		 * The operation that is capable to revert/compensate the effect of this operation
		 */
		('compensated-by' compensatedBy+=OperationRef )?
		('realizes-capability' '{'(realizedCapabilities+=CapabilityRef)+ '}')?	
		('supports-capability' '{'(supportedCapabilities+=CapabilityRef)+ '}')?	
		('tags' (tags+=[semanticsDsl::Tag])+)*
	'}';

Response: 
	('response-code')? responseCode = ReturnCode ('content-type' (contentType += STRING)(',' contentType += STRING)*)? '{'
		(((return+=Parameter) (',' return+=Parameter)*) | (return+=SingleReturnParameter))?
	'}'
;
ErrorResponse: 
	('response-code')? responseCode = ReturnCode ('content-type' (contentType += STRING)(',' contentType += STRING)*)? ('{' 
		(exception+=ExceptionRef)?(',' exception+=ExceptionRef)*
	'}')?
;

ReturnCode: IntReturnCode | StringReturnCode;
IntReturnCode: returnCode=INT;
StringReturnCode: returnCode=STRING;

AbstractParameter: Parameter|SingleReturnParameter;
/*
 * An operation (return) parameter
 * If the modifier 'as-attachment-ref' is set, the parameter will be transferred out-of-band
 * as an attach
 */	 
Parameter:
	(optional?='optional')? name=ID ':' type=TypeRef  (attachmentRef?='as-attachment-ref' )? PatternRestriction? DefaultValue?;
SingleReturnParameter returns Parameter:
	(optional?='optional')? type=TypeRef  (attachmentRef?='as-attachment-ref' )? DefaultValue?;

/*
 * Declared service operation exceptions
 */
Exception:
	(deprecated?='@deprecated')? 
	'exception' name=ID ('extends' superException=ExceptionRef)? '{' 
		'metadata' '{'
			version=Version  
			('replaces' replaces=ExceptionRef )?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName] )?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(lifecycleTrigger=LifecycleTrigger)?
			('provided-contract' providedContractUrl=STRING)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+ )*
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(properties+=SimpleAttribute)*
	'}';

ExceptionRef:
	exception=[Exception|QualifiedName] versionRef=VersionRef;
	
/* the message exchange pattern */
enum MEP : 
	sync | async | oneway | fireForget='fire+forget' | deferredResponse='deferred' | syncAsync='sync+async'; 

enum Verb:
	POST | GET | PUT | DELETE | PATCH | HEAD
;
/*
 * Specification of transactional behaviour
 */
enum TransactionSpec:
	requiresNew = "requires-new" | requires='requires' | mandatory
;
	
	
enum Statefulness: 
	/* same result, no matter how often the operation has been called with the same parameters*/
	idempotent | 
	stateless |
	/* Operation acts as if stateless, however a state transiently exists */ 
	transientState="transient-state"| 
	stateful
;

enum Persistence: none | read | write | readWrite = "read/write";

/* references a service applying a version constraint */
ServiceRef:
	service=[Service|QualifiedName] versionRef=VersionRef;

/*
 * reference to a required service in order to fulfill a service capability
 */
RequiredInterfaceRef:
	interfaceRef=[Interface|QualifiedName] versionRef=VersionRef (RequiredTraitCallFragment)?;
/*
 * A reference to a service operation with a version constraint
 */	
OperationRef:
	operation=[AbstractOperation|QualifiedName] versionRef=VersionRef;
	
/*
 * A reference to a service operation without a version constraint, 
 * taking the version constraint from the service reference defined in the same context
 */	
SimpleOperationRef: 
	operation=[AbstractOperation|QualifiedName];

fragment RequiredTraitCallFragment: '{'
		'calling' '{'
			(calledOperations+=CallOperationRef) (',' calledOperations+=CallOperationRef)* 
		'}'
	'}'
;

CallOperationRef: operation=[AbstractOperation|QualifiedName] ('handle-response-with' responseHandler=OperationRef)?;

/* defines the execution of an operation in terms of considered parameters */
ConsiderationSpec:
	'considers' '{' 
		consideredParam+=ConsiderationParameterRef+ 
	'}';
	
	
ConsiderationParameterRef:
	'parameter' param=[Parameter|QualifiedName] ((flat?='flat-only') | ('->' '{' (propertyRef+=ConsiderationPropertyRef
	)+ '}'));
	
ConsiderationPropertyRef:
	SimpleConsiderationPropertyRef | ComplexConsiderationPropertyRef
;

SimpleConsiderationPropertyRef:
	property=[Property|QualifiedName]
;

		
ComplexConsiderationPropertyRef:
	
	(parentProperty = [Property|QualifiedName] 
	( 
		(flat?='flat-only') | 
		'->' '{' 
			(nestedPropertyRef+=ConsiderationPropertyRef
			)+
		'}')
	)
;

/* Reference to a message used in service messages */
MessageHeaderRef:
	header = [profileDsl::MessageHeader|QualifiedName] versionRef=VersionRef;

/*
 * References a business capability
 */
CapabilityRef:
	capability=[businessDsl::Capability|QualifiedName] versionRef=VersionRef;
	
Link: name=ID (('links-to' link=OperationRef)|('links-to-url' url=STRING));

//AbstractEvent: Event | Command;

/*
 * Event, that is not scoped to an operation
 */
Event:
	(deprecated?='@deprecated')? 
	'event' name=ID ('extends' extends=EventRef)?  '{'
		'metadata' '{'
			version=Version  
			('replaces' replaces=EventRef )?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName] )?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(lifecycleTrigger=LifecycleTrigger)?
			('provided-contract' providedContractUrl=STRING)?
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+ )*
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(parameter+=Parameter )*
	'}';

EventRef:
	event=[Event|QualifiedName] versionRef=VersionRef;
/*
 * Event, that is not scoped to an operation
 */
Command:
	(deprecated?='@deprecated')? 
	'command' name=ID ('extends' extends=CommandRef)?'{'
		'metadata' '{'
			version=Version  
			('replaces' replaces=CommandRef )?
			('lifecycle-state' state=[profileDsl::LifecycleState | QualifiedName] )?
			(governanceApproval=GovernanceApproval)?
			(governanceDecisions+=GovernanceDecision)*
			(lifecycleTrigger=LifecycleTrigger)?
			('provided-contract' providedContractUrl=STRING)?
			(additionalInfo=AdditionalInformation)?
			('contact-info' '{'
				('owner' owner=[basedsl::User|QualifiedName])?
				('responsible' responsible += [basedsl::User|QualifiedName])*
				('budgeting' budgeting = [basedsl::User|QualifiedName])?
			'}')?
			('tags' (tags+=[semanticsDsl::Tag])+ )*
			('subscriptions' '{'
				subscriptions += [basedsl::User|QualifiedName]*
			'}')?
		'}'
		(parameter+=Parameter )*
	'}';


CommandRef:
	command=[Command|QualifiedName] versionRef=VersionRef;

/*
 * Channel used to publish events to or to watch for events
 */
Channel:
	(deprecated?='@deprecated')? 
	(isTopic?='topic')? 'channel' name=QualifiedName  '{'
		('subscribers' '{'
			subscribers += AssetRef*
		'}')?
		('publishers' '{'
			publisher += AssetRef*
		'}')?
		('transmits-events'  '{'
			transmitsEvents += EventRef*
		'}')?
		('transmits-commands'  '{'
			transmitsCommands += CommandRef*
		'}')?
	'}';
	
	
/**
 * The Visibility of a service etc. <br/><br/>
 * 
 * <ul>
 * 	<li>public:	usable by anyone, potentially remotely available </li>
 * 	<li>domain:	usable by all services in the same namespace or a subnamespace, potentially remotely available </li>
 * 	<li>private:	for internal use only, not remotely available on an ESB or API Gateway </li>
 * </ul>
 */	
enum Visibility : public | domain | private; 

/* 
 * reflects SOA governance approval decisions
 * voting until date will be part of the review ballots captured in the side store
 */
GovernanceApproval:
	'governance-approval' '{'
		decision=ApprovalDecision 
		('justification-doc' justificationOrDocURL=STRING )?
		('decision-date' approvalDate=(STRING|DATE) )? 
		('decision-by' approvedBy=STRING )?
	'}'; 
	
enum ApprovalDecision: no | yes | temporarily_tolerated="temporarily-tolerated" | tolerated | denied;

LifecycleTrigger:
	{LifecycleTrigger}
	/*
	 * Date, when the asset will be or has been released to production
	 */
	('release-on' releaseDate = (STRING|DATE) )?
	/*
	 * Date, when the service will be or was put out of service
	 */
	('retirement-on' retirementDate = (STRING|DATE))?
;
