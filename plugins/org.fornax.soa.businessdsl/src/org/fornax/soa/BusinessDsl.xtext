/*
Copyright (c) 2010-2011 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
    Krunoslav Markotic	- reviews
    Liane Lenke			- reviews
*/
grammar org.fornax.soa.BusinessDsl with org.fornax.soa.basedsl.SOABaseDsl

generate businessDsl "http://www.fornax.org/soa/BusinessDsl"

import "platform:/resource/org.fornax.soa.semanticsdsl/src-gen/org/fornax/soa/SemanticsDsl.ecore" as  semantics

BusinessModel hidden(WS):
	(imports+=BizImport)*
	(organizations+=Organization)*
	(domains+=Domain)*;
	
	
BizImport hidden(WS):
	'import' importURI=STRING;
	
Organization hidden(WS):
	'org' name=QualifiedName '{'
		'metadata' '{'
			('competence' competence=STRING)?
			('head' bizLead=[Member|QualifiedName])?
		'}'
		(businessFunctions+=BusinessFunction)*
		(subOrganizations+=[Organization|QualifiedName])*
		(members+=Member)*
	'}';
	
Domain hidden(WS):
	'domain' name=QualifiedName '{'
		'metadata' '{'
			('competence' competence=STRING)?
			('head' bizLead=[Member|QualifiedName])?
			('technical lead' techLead=[Member|QualifiedName])?
			('service management lead' svcMgmtLead=[Member|QualifiedName])?
			('tags' (tags+=[semantics::Tag])+)?
		'}'

		(domains+=Domain)*
		(capabilityGroups+=CapabilityGroup)*
		(capabilites+=Capability)*
	'}';
	
/**
 * A business function operated by an organization.
 */
BusinessFunction hidden(WS):
	'business-function' name=ID '{' 
		'metadata' '{'
			'title' title=STRING
			('docURL' docURL=STRING)? 
			('tags' (tags+=[semantics::Tag])+)?
		'}'

		'uses' '{'
			(capabilties+=CapabilityRef)*
		'}'
	'}' ; 
	
/**
 * A group of business capabilities. Capability Groups are used to build capability hierarchies.
 */
CapabilityGroup :
	'capability-group' name=ID '{'
		description = STRING
		(capabilities+=Capability)*
		(capabilityGroups+=CapabilityGroup)*
	'}'
;

/**
 * A business capability
 * 
 * A business capability is a particular ability or capacity that a business may possess or exchange 
 * to achieve a specific purpose or outcome. A capability describes what the business 
 * does (outcomes and service levels) that creates value for customers; for example, pay employee or ship product. 
 * A business capability abstracts and encapsulates the people, process/procedures, technology, and information 
 * into the essential building blocks needed to facilitate performance improvement and redesign analysis.
 * 
 * @see http://msdn.microsoft.com/en-us/library/aa479368.aspx
 */
Capability: SimpleCapability | CapabilityVariant;

SimpleCapability hidden(WS):
	(external?='external')? 'capability' name=ID '{'
		version=Version
		'short-description' shortDescription=STRING
		('description' description=STRING)?
		'automation-level' automationLevel=CapabilityAutomationLevel
		('business-value' businessValue=STRING)
		('owner' owner=[Member|QualifiedName])?
		('docURL' docURL=STRING)? 
		(	
			'inputs' '{'
				(inputs+=Input)*
			'}'
		)?
		(
			'outcomes' '{'
				(outcomes+=Outcome)*
			'}'
		)?
		('uses' '{'
			(uses+=CapabilityRef)+
		'}'
		)? 
		('tags' tags+=[semantics::Tag|QualifiedName])*
	'}';

/**
 * A variant of a capability describing what is varied.
 */
CapabilityVariant:
	(external?='external')? 'capability-variant' name=ID 'based on' basedOnCapabilty=[Capability|QualifiedName] '{'
		version=Version
		'short-description' shortDescription=STRING
		('description' description=STRING)?
		'automation-level' automationLevel+=CapabilityAutomationLevel
		('business-value' businessValue=STRING)
		('owner' owner=[Member|QualifiedName])?
		('docURL' docURL=STRING)?
		('adding' '{'
			(additions += CapabilityVariation)+
		'}')?
		('replacing' '{'
			(replacements += CapabilityReplacement)+
		'}')?
		('removing' '{'
			(removals += CapabilityVariation)+
		'}')?
		(	
			'inputs' '{'
				(inputs+=Input)*
			'}'
		)?
		(
			'outcomes' '{'
				(outcomes+=Outcome)*
			'}'
		)?
		('uses' '{'
			(uses+=CapabilityRef)+
		'}'
		)? 
		('tags' tags+=[semantics::Tag|QualifiedName])*
	'}';

CapabilityVariation:
 	varying=CapabilityRef ('{'
		'context' context=STRING
	'}')?
 	;
 	
CapabilityReplacement:
 	varying=CapabilityRef 'with' replacement=CapabilityRef ('{'
		'context' context=STRING
	'}')?
 	;
 	
CapabilityRef:
	capability=[Capability|QualifiedName] versionRef=VersionRef;
	
enum CapabilityAutomationLevel: automated | semiAutomated = 'semi-automated' | humanAssisted = 'human-assisted' | manual;

/**
 * The input of a capability
 */
Input: 
	'input' description=STRING
;

/**
 * The outcome of a capability
 */
Outcome:
	'outcome' '{'
		description=STRING
	'}'
;

	
Member hidden(WS):
	'member' name=ID '{'
		'fullname' fullname=STRING
		'email' email=STRING
	'}';
