/*
Copyright (c) 2010-2019 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
*/
grammar org.xkonnex.repo.dsl.basedsl.BaseDsl with org.eclipse.xtext.common.Terminals

generate baseDsl "http://www.xkonnex.org/repo/dsl/basedsl/BaseDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

/* Java like imports */
Import: 
	'import' importedNamespace=QualifiedNameWithWildCard;

/* 
 * Version numbers, some post-fixes are treated in a special way:
 * <ul>
 * 	<li>Final:		treated as if the version number ends with ".0"</li>
 * 	<li>RC:			release candidate, may end with a number</li>
 * 	<li>CR:			release candidate (synonym for RC), may end with a number</li></li>
 * 	<li>Beta:		beta, may end with a number</li>
 * 	<li>Alpha:		alpha, may end with a number</li>
 * 	<li>Milestone:	milestone release, may end with a number</li>
 * </ul>
 */
VersionId: INT('.'INT)*(('.'|'-')ID)*;

VersionedName: {VersionedName} QualifiedName|ID (WS)* version=VersionId;

/*
 * A version qualifier for versioned assets
 */
Version:
	'version' (version=VersionId) (frozen?='frozen')?;

/*
 * Version constraints applied to referenced assets
 */
VersionRef:
	MinVersionRef | MaxVersionRef | LowerBoundRangeVersionRef | MajorVersionRef | FixedVersionRef;

/*
 * Assets, where the version is lesser than maximal version: asset version >= minVersion
 */	
MinVersionRef:
	('minVersion') minVersion=VersionId;

/*
 * Assets, that match a minimal version: asset version >= minVersion
 */	
MaxVersionRef:
	('maxVersion') maxVersion=VersionId;

/*
 * Matches version in the half open range [minVersion, maxVersion)
 */	
LowerBoundRangeVersionRef:
	('minVersion' '>=') minVersion=VersionId ',' ('maxVersion' '<') maxVersion=VersionId;

/*
 * Constraint matches assets with any version with the same major version, i.e. the first INT until the first '.' is equal
 */
MajorVersionRef:
	'majorVersion' majorVersion=INT;

/*
 * Constraint that matches an asset version  explicitly
 */
FixedVersionRef:
	'fixedVersion' fixedVersion=VersionId;

QualifiedNameWithWildCard:
	QualifiedName '.*'?;

QualifiedName:
	ID (('.') ID)*;
	
TagName: ('#')? QualifiedName;
	

Email returns ecore::EString: ID (('.') ID)* '@' ID (('.') ID)*;

EmailOrUsername returns ecore::EString: STRING|Email;

/*
 * Expresses SOA governance decisions
 * voting until date will be part of the review ballots captured in the side store
 */
GovernanceDecision:
	'governance-decision' '{'
		('subject' subject=STRING)
		('decision-result' decisionResult=GovernanceDecisionResult)
		('specific-result' specificResult=(TEXT|STRING))?
		('justification-doc' justificationOrDocURL=(TEXT|STRING) )?
		('decision-date' approvalDate=(STRING|DATE) )? 
		('decision-by' approvedBy=EmailOrUsername)?
		('effective-date' effectiveDate=(STRING|DATE))?
	'}'
;

ContactInfo:
			{ContactInfo} 'contact-info' '{'
				('owner' owner=EmailOrUsername)?
				('responsible' responsible += EmailOrUsername)*
				('budgeting' budgeting = EmailOrUsername)?
			'}';

enum GovernanceDecisionResult:
	pending | accepted | denied | specific
;

	

AbstractType: {AbstractType} name=ID;

AdditionalInformation: 
	'additional-information' '{'
		infoItems += InfoItem+
	'}'
;
	
InfoItem:
	'location' documentLocation=(TEXT|STRING) ('section' section=(TEXT|STRING))? ('page' page=(TEXT|STRING))?
;
	
AssetRef: asset = [ecore::EObject|QualifiedName] (versionRef=VersionRef)?;

Namespace:
	name=QualifiedName (prefix=STRING)? (uri=STRING)?
;

Assignment:
	feature=[jvmTypes::JvmIdentifiableElement|QualifiedName] '=' value=Value;

Value:
	NestedComponent |
	StringValue |
	BooleanValue |
	IntValue |
	DateValue |
	DateTimeValue |
	TimeValue |
	EnumLiteralValue |
	Reference
;
SimpleValue: StringValue |
	BooleanValue |
	IntValue |
	DateValue |
	DateTimeValue |
	TimeValue |
	TimeValue |
	EnumLiteralValue;
BooleanValue:
	{BooleanValue} ('false' | isTrue?='true');
DateValue:
	value=DATE
;	
DateTimeValue:
	value=DATETIME
;	
TimeValue:
	value=TIME
;	
EnumLiteralValue: 
	value = [jvmTypes::JvmEnumerationLiteral|QualifiedName]
;

Reference:
//	referable=[Component|QualifiedName];
	'-->>' referable = [ecore::EObject|QualifiedName] (versionRef=VersionRef)?;

StringValue:
	value = STRING
;

IntValue : value=INT;

Component:
	{Component} (name=QualifiedName 'type')? (type=[jvmTypes::JvmType|QualifiedName])? '{'
		assignment+=Assignment* 
	'}';
	
NestedComponent returns Component:
	{Component} '{'
		assignment+=Assignment* 
	'}';
	
AuthToken : (optional?='optional')? kind=AuthTokenKind ;
AuthTokenKind: type=[jvmTypes::JvmType|QualifiedName];

HashAlgorithm : (optional?='optional')? (useBase64?="Base64-encoded")? kind=HashAlgorithmKind ;
HashAlgorithmKind: type=[jvmTypes::JvmType|QualifiedName] ('{'
		assignment+=Assignment* 
	'}')?;

SigningAlgorithm: type=[jvmTypes::JvmType|QualifiedName] ('{'
		assignment+=Assignment* 
	'}')?;

CipherAlgorithmKind: type=[jvmTypes::JvmType|QualifiedName] ('{'
		assignment+=Assignment* 
	'}')?;

AnyBaseDslComponent returns Component: HashAlgorithmKind | SigningAlgorithm | CipherAlgorithmKind | AuthTokenKind;

License: 
	'license' name=STRING ('{'
		'license-url' licenseUrl=STRING
	'}')?
;

Double returns ecore::EDouble: INT'.'INT;
IntAsDouble returns ecore::EDouble: INT;
Percent: value=(IntAsDouble|Double) '%';

fragment DefaultValue: 'default' defaultValue=STRING;

terminal DATE: ('0'..'9')('0'..'9')('0'..'9')('0'..'9')'-'('0'..'1')('0'..'9')'-'('0'..'3')('0'..'9');
terminal DATETIME: ('0'..'9')('0'..'9')('0'..'9')('0'..'9')'-'('0'..'1')('0'..'9')'-'('0'..'3')('0'..'9')'T'('0'..'2')('0'..'9')':'('0'..'5')('0'..'9')':'('0'..'5')('0'..'9');
terminal TIME: ('0'..'2')('0'..'9')':'('0'..'5')('0'..'9')':'('0'..'5')('0'..'9');

terminal TEXT: "`" -> "`";
