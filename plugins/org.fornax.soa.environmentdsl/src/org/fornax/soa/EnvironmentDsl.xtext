/*
Copyright (c) 2010-2011 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
*/
grammar org.fornax.soa.EnvironmentDsl with org.eclipse.xtext.common.Terminals

generate environmentDsl "http://www.fornax.org/soa/EnvironmentDsl"

import "platform:/resource/org.fornax.soa.semanticsdsl/src-gen/org/fornax/soa/SemanticsDsl.ecore" as  semanticsDsl

EnvModel:
	(imports+=EnvImport)*
	(users+=User)*
	(environments+=Environment)*;
	
EnvImport : EnvNsImport | EnvFileImport; 

EnvNsImport:
	'import' importedNamespace=QualifiedNameWithWildCard ; 
	
EnvFileImport:
	'import' importURI=STRING ;
	
Environment:
	'environment' name=ID 'of type' (type=EnvironmentType) '{'
		(hosts+=Host)*
		(servers+=Server)*
		('default' 'ESB' defaultESB=[ESB])? 
		('default' 'Registry' defaultRegistry=[Registry])? 
		('default' 'AppServer' defaultAppServer=[AppServer])? 
		('default' 'Broker' defaultBroker=[Broker])? 
	'}';
	
enum EnvironmentType: Prod = "Production-Environment" | Staging="Staging-Environment" | Test="Test-Environment" | Dev="Development-Environment" | Local="Local-Environment" | Other="Other-Environment";

Server: ESB | Registry | ProcessServer | AppServer | Broker | WebServer | Database | SAP;

ESB: 
	'esb' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=[User|QualifiedName] )? 
		'host' host=[Host|QualifiedName] 
		('dir' home=STRING )?
		('deployDir' deployDir=STRING )?
		('baseUrl' baseUrl=STRING )?
		('soapUrl' soapUrl=STRING )?
		('adminUrl' adminUrl=STRING )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';

Registry:
	'registry' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' admins+=[User|QualifiedName] )? 
		'host' host=[Host|QualifiedName] 
		('dir' home=STRING )?
		('deployDir' deployDir=STRING )?
		('baseUrl' baseUrl=STRING )?
		('adminUrl' adminUrl=STRING )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		('kind' kind=RegistryKind )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';
	
enum RegistryKind : UDDI | HTTP | File | Native;

RegistryService: JNDIRegistry | COSNamingRegistry | RMIRegistry | IOR | UDDIRegistry;

JNDIRegistry: 
	'jndi' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
		'jndi-url' url=STRING 
		('InitialContextFactory' initialContextFactory = STRING)?
		(clustered?='clustered')?
	'}'
;

COSNamingRegistry:
	'COS-Naming' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
		(clustered?='clustered')?
	'}'
;

RMIRegistry:
	'RMI-Registry' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
	'}'
;

IOR:
	'Corba-IOR' '{'
		'port' port = INT
		('secure-port' securePort = INT)?
	'}'
;

UDDIRegistry:
	'UDDI' '{'
		'url' url = STRING
		('secure-url' secureUrl = STRING)?
	'}'
;
	
AppServer: 
	'appserver' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=[User|QualifiedName])+ )? 
		'host' host=[Host|QualifiedName] 
		('dir' home=STRING )?
		('deployDir' deployDir=STRING  )?
		('baseUrl' baseUrl=STRING  )?
		('adminUrl' adminUrl=STRING  )?
		(dataSources+=DataSource)*
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';

WebServer: 
	'webserver' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=[User|QualifiedName])+ )? 
		'host' host=[Host|QualifiedName] 
		('dir' home=STRING )?
		('deployDir' deployDir=STRING  )?
		('baseUrl' baseUrl=STRING  )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';
	
	
ProcessServer:
	'processserver'  name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=[User|QualifiedName])+ )? 
		'host' host=[Host|QualifiedName] 
		('dir' home=STRING )?
		('deployDir' deployDir=STRING  )?
		('deployUser' deployUser=[User] )?
		('baseUrl' baseUrl=STRING  )?
		('adminUrl' adminUrl=STRING  )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';

Broker: 
	'broker' name=ID '{'
		('identifier' dslCode=STRING )? 
		('admins' (admins+=[User|QualifiedName])+ )? 
		'host' host=[Host|QualifiedName] 'dir' home=STRING 
		('deployDir' deployDir=STRING )?
		('baseUrl' baseUrl=STRING )? 
		('adminUrl' adminUrl=STRING )?
		('server-type' serverType=STRING )?
		('server-version' serverVersion=STRING )?
		(
			'connectors' '{'
				(connectors+=Connector)+
			'}'
		)?
		(
			'registries' '{'
				(registries+=RegistryService)+
			'}'
		)?
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}'; 

Database:  
	'database' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=[User|QualifiedName])+ )? 
		'host' host=[Host|QualifiedName]  
		('dir' home=STRING )? 
		('dataDir' dataDir=STRING )? 
		('adminUrl' adminUrl=STRING  )?
		('jdbcUrl' jdbcUrl=STRING )?
		('database-type' serverType=STRING )?
		('database-version' serverVersion=STRING )?
		('{' (schema+=DatabaseSchema)+ '}')? 
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';

SAP:  
	'sap' name=ID '{'
		('identifier' dslCode=STRING )?
		('admins' (admins+=[User|QualifiedName])+ )? 
		'host' host=[Host|QualifiedName] 
		'system-id' systemId = INT 
		('dir' home=STRING )?
		('dataDir' dataDir=STRING )?
		('adminUrl' adminUrl=STRING )? 
		('{' (client+=SapClient)+ '}')? 
		('tags' (tags+=[semanticsDsl::Tag])+ )*
	'}';
	
SapClient: 
	'client' name=ID '{'
		'client-ID' client=INT 
		'owner' owner=STRING 
		('accountingArea' accountingArea=STRING)?
	'}';
	
DataSource:
	'data-source' name=ID '{'
		'jndi-name' jndiName=STRING 
		'schema' schema=[DatabaseSchema|QualifiedName] 
	'}';
	
	
enum DatabaseKind : Other | Oracle | DB2 | MSSQL | MySQL | Postgres | Sybase | HyperSonic | Ingres | Adabas | Derby | MongoDB | Hadoop | CouchDB | Neo4J;

DatabaseSchema: 
	'schema' name=ID 'owner' owner=STRING;


Connector:
	SOAPHTTP | SOAPJMS | REST | EJB | JMS | AMQP | IIOP | RMI | Tuxedo | RFC | FTP | IMAP | POP3 | SMTP | HTTP | TCP
;

SOAPHTTP:
	{SOAPHTTP}
	(isDefault?='default')? 'SOAP/HTTP' name=ID '{'
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		('context-root' contextRoot=STRING)?
		('implementation' implementation=STRING)?
	'}';

SOAPJMS:
	(isDefault?='default')? 'SOAP/JMS' name=ID '{'
		('versions' (version+=STRING)+)?
		'port' port=INT
		('security-protocol' securityProtocol=SecurityProtocol)?
		connectionSpec=JCAConnectionSpec
		('implementation' implementation=STRING)?
	'}';

REST:
	{REST}
	(isDefault?='default')? 'REST' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		('context-root' contextRoot=STRING)?
	'}';

EJB:
	{EJB}
	(isDefault?='default')? 'EJB' name=ID '{'
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}'
;

IIOP:
	{IIOP}
	(isDefault?='default')? 'IIOP' name=ID '{'
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(connectionSpec=JCAConnectionSpec)?
	'}'
;

RMI:
	{RMI}
	(isDefault?='default')? 'RMI' name=ID '{'
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(connectionSpec=JCAConnectionSpec)?
	'}'
;

Tuxedo:
	{Tuxedo}
	(isDefault?='default')? 'Tuxedo' name=ID '{'
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}'
;

JMS:
	(isDefault?='default')? 'JMS' name=ID '{'
		('versions' (version+=STRING)+)?
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		connectionSpec=JCAConnectionSpec
		('implementation' implementation=STRING)?
	'}';

AMQP:
	{AMQP}
	(isDefault?='default')? 'AMQP' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(connectionSpec=JCAConnectionSpec)?
		('implementation' implementation=STRING)?
	'}'
;

RFC: 
	{RFC}
	(isDefault?='default')? 'RFC' name=ID '{'
		('port' port=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		(connectionSpec=JCAConnectionSpec)?
	'}';

FTP:
	{FTP}
	(isDefault?='default')? 'FTP' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}';

IMAP:
	{IMAP}
	(isDefault?='default')? 'IMAP' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}';

POP3:
	{POP3}
	(isDefault?='default')? 'POP3' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort=INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}';

JCAConnectionSpec:
	(isDefault?='default')? 'JCA-connection-spec' name=ID '{'
		'connection-factory-JNDI-name' connectionFactoryJNDI = STRING
		('connection-factory-class' connectionFactoryClass = STRING)?
	'}';

SMTP:
	{SMTP}
	(isDefault?='default')? 'SMTP' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort = INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}';

HTTP:
	{HTTP}
	(isDefault?='default')? 'HTTP' name=ID '{'
		('port' port=INT)?
		('secured-port' securedPort = INT)?
		('security-protocol' securityProtocol=SecurityProtocol)?
		('context-root' contextRoot = STRING)?
	'}';

TCP:
	(isDefault?='default')? 'TCP' name=ID '{'
		'port' port = INT
		('security-protocol' securityProtocol=SecurityProtocol)?
	'}';
	
		
Host:
	'host' name=ID '{'
		'full-qualified-name' fqn=STRING 
		('ip-address' ip=STRING)? 
		('identifier' dshCode=STRING )?
		('admins' admins+=[User|QualifiedName])?
	'}';
	
User: 
	'user' name=ID '{'
		('firstname' firstname=STRING 'lastname' lastname=STRING )? 
		'email' email=STRING 
		('phone' phone=STRING )?
	'}';
	
enum SecurityProtocol: NONE | SSLv2 | SSLv3 | TLS | IPSec;
	
QualifiedNameWithWildCard:
	QualifiedName '.*'?;

QualifiedName:
	ID ('.' ID)*;
