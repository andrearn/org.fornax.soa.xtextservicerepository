/*
Copyright (c) 2010-2011 André Arnold and others.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    André Arnold 		- initial API and implementation
*/
grammar org.fornax.soa.basedsl.SOABaseDsl with org.eclipse.xtext.common.Terminals

generate sOABaseDsl "http://www.fornax.org/soa/basedsl/SOABaseDsl"

Import: 
	'import' importedNamespace=QualifiedNameWithWildCard;

VersionId: INT('.'INT)*('.'ID)?;

VersionedName: {VersionedName} QualifiedName|ID (WS)* version=VersionId;

Version:
	'version' (version=VersionId) ;
	
VersionRef:
	MinVersionRef | MaxVersionRef | LowerBoundRangeVersionRef | MajorVersionRef | FixedVersionRef;
	
MinVersionRef:
	('minVersion') minVersion=VersionId;
	
MaxVersionRef:
	('maxVersion') maxVersion=VersionId;
	
LowerBoundRangeVersionRef:
	('minVersion' '>=') minVersion=VersionId ',' ('maxVersion' '<') maxVersion=VersionId;

MajorVersionRef:
	'majorVersion' majorVersion=INT;
	
FixedVersionRef:
	'fixedVersion' fixedVersion=VersionId;

QualifiedNameWithWildCard:
	QualifiedName '.*'?;

QualifiedName:
	ID ('.' ID)*;
	
Qualifier: 
	'qualifier' qualifierName=QualifiedName;
	
MultiQualifier:
	'qualified-by' (isOneOf?='one-of')? qualifiedName+=QualifiedName (qualifiedName+=QualifiedName)*
;

AbstractType: {AbstractType} name=ID;
