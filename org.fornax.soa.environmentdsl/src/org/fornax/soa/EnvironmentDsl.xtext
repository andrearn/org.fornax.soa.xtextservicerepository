grammar org.fornax.soa.EnvironmentDsl with org.eclipse.xtext.common.Terminals

generate environmentDsl "http://www.fornax.org/soa/EnvironmentDsl"

import "platform:/resource/org.fornax.soa.semanticsdsl/src-gen/org/fornax/soa/SemanticsDsl.ecore" as  semanticsDsl

EnvModel  hidden(WS):
	(imports+=EnvImport)*
	(users+=User)*
	(environments+=Environment)*;
	
EnvImport : EnvNsImport | EnvFileImport; 

EnvNsImport :
	'import' importedNamespace=QualifiedNameWithWildCard ";"; 
	
EnvFileImport hidden(WS):
	'import' importURI=STRING ';';
	
Environment :
	'environment' name=ID 'of type' (type=EnvironmentType) '{'
		(hosts+=Host)*
		(servers+=Server)*
		('default ESB' defaultESB=[ESB])? 
		('default AppServer' defaultAppServer=[AppServer])? 
		('default Broker' defaultBroker=[Broker])? 
	'}';
	
enum EnvironmentType : Prod = "Production-Environment" | PreProd="Pre-Production-Environment" | Test="Test-Environment" | Dev="Development-Environment" | Other="Other-Environment";

Server : ESB | IvyServer | AppServer | Broker | Database | SAP;

ESB : 
	'esb' name=ID '{'
		('DSL-code' dslCode=STRING ";")?
		('admins' admins+=[User|QualifiedName] ";")? 
		'host' host=[Host|QualifiedName] ";"
		('dir' home=STRING ";")?
		('deployDir' deployDir=STRING ";")?
		('baseUrl' baseUrl=STRING ";")?
		('soapUrl' soapUrl=STRING ";")?
		('adminUrl' adminUrl=STRING ";")?
		('server-type' serverType=STRING ';')?
		('server-version' serverVersion=STRING ';')?
		('tags' (tags+=[semanticsDsl::Tag])+ ';')*
	'}';
	
AppServer : 
	'appserver' name=ID '{'
		('DSL-code' dslCode=STRING ";")?
		('admins' (admins+=[User|QualifiedName])+ ";")? 
		'host' host=[Host|QualifiedName] ";"
		('dir' home=STRING ";")?
		('deployDir' deployDir=STRING  ";")?
		('baseUrl' baseUrl=STRING  ";")?
		('adminUrl' adminUrl=STRING  ";")?
		(jndi=JNDI)?
		(dataSources+=DataSource)*
		('server-type' serverType=STRING ';')?
		('server-version' serverVersion=STRING ';')?
		('tags' (tags+=[semanticsDsl::Tag])+ ';')*
	'}';
	
JNDI : 
	(clustered?='clustered')? 'jndi' name=ID '{'
		'jndi-url' url=STRING ';'
	'}';
	
IvyServer :
	'ivyserver'  name=ID '{'
		('DSL-code' dslCode=STRING ";")?
		('admins' (admins+=[User|QualifiedName])+ ";")? 
		'host' host=[Host|QualifiedName] ";"
		('dir' home=STRING ";")?
		('deployDir' deployDir=STRING  ";")?
		('deployUser' deployUser=[User] ";")?
		('baseUrl' baseUrl=STRING  ";")?
		('adminUrl' adminUrl=STRING  ";")?
		('tags' (tags+=[semanticsDsl::Tag])+ ';')*
	'}';

Broker : 
	'broker' name=ID '{'
		('DSL-code' dslCode=STRING ";")? 
		('admins' (admins+=[User|QualifiedName])+ ";")? 
		'host' host=[Host|QualifiedName] 'dir' home=STRING ";"
		('deployDir' deployDir=STRING ";")?
		('baseUrl' baseUrl=STRING ";")? 
		('adminUrl' adminUrl=STRING ";")?
		('server-type' serverType=STRING ';')?
		('server-version' serverVersion=STRING ';')?
		('tags' (tags+=[semanticsDsl::Tag])+ ';')*
	'}'; 

Database :  
	'database' name=ID '{'
		('DSL-code' dslCode=STRING ";")?
		('admins' (admins+=[User|QualifiedName])+ ";")? 
		'host' host=[Host|QualifiedName] ";" 
		('dir' home=STRING ";")? 
		('dataDir' dataDir=STRING ";")? 
		('adminUrl' adminUrl=STRING ";" )?
		('jdbcUrl' jdbcUrl=STRING ";")?
		('database-type' serverType=STRING ';')?
		('database-version' serverVersion=STRING ';')?
		('{' (schema+=DatabaseSchema)+ '}')? 
		('tags' (tags+=[semanticsDsl::Tag])+ ';')*
	'}';

SAP :  
	'sap' name=ID '{'
		('DSL-code' dslCode=STRING ";")?
		('admins' (admins+=[User|QualifiedName])+ ";")? 
		'host' host=[Host|QualifiedName] ";" 
		('dir' home=STRING ";")?
		('dataDir' dataDir=STRING ";")?
		('adminUrl' adminUrl=STRING ";")? 
		('{' (client+=SapClient)+ '}')? 
		('tags' (tags+=[semanticsDsl::Tag])+ ';')*
	'}';
	
SapClient : 
	'client' name=ID '{'
		'client-ID' client=INT ';'
		'owner' owner=STRING ';'
		('accountingArea' accountingArea=STRING)?
	'}';
	
DataSource :
	'data-source' name=ID '{'
		'jndi-name' jndiName=STRING ';'
		'schema' schema=[DatabaseSchema|QualifiedName] ';'
	'}';
	
enum DatabaseKind : Oracle | DB2 | MSSQL | MySQL | Postgres;

DatabaseSchema: 
	'schema' name=ID 'owner' owner=STRING;
	
Host :
	'host' name=ID '{'
		'full-qualified-name' fqn=STRING 
		('ip-address' ip=STRING)? 
		('DHS-code' dshCode=STRING )?
		('admins' admins+=[User|QualifiedName])?";"
	'}';
	
User : 
	'user' name=ID '{'
		('firstname' firstname=STRING 'lastname' lastname=STRING ";")? 
		'email' email=STRING ";"
		('phone' phone=STRING ";")?
	'}';
	
QualifiedNameWithWildCard:
	QualifiedName '.*'?;

QualifiedName:
	ID ('.' ID)*;
